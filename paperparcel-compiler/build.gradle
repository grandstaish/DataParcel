apply plugin: 'java'
apply plugin: 'idea'

def logger = new com.android.build.gradle.internal.LoggerWrapper(project.logger)
def sdkHandler = new com.android.build.gradle.internal.SdkHandler(project, logger)
for (File file : sdkHandler.sdkLoader.repositories) {
  repositories.maven {
    url = file.toURI()
  }
}

sourceCompatibility = rootProject.ext.sourceCompatibilityVersion
targetCompatibility = rootProject.ext.targetCompatibilityVersion

task fatJar(type: Jar) {
  baseName = project.name + '-all'
  from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
  with jar
}

test {
  testLogging {
    events "passed", "skipped", "failed", "standardOut", "standardError"
  }
}

dependencies {
  compile project(':paperparcel-api')
  compile deps.autovalue
  compile deps.autoservice
  compile deps.autocommon
  compile deps.javapoet
  compile deps.javaFormat

  testCompile files('libs/android.jar')
  testCompile deps.junit
  testCompile deps.truth
  testCompile deps.compiletesting
  testCompile files(org.gradle.internal.jvm.Jvm.current().getToolsJar())
}

tasks.withType(Test) {
  testLogging {
    events "passed", "skipped", "failed"
  }
}

apply from: rootProject.file('gradle/bintray.gradle')
apply from: rootProject.file('gradle/snapshots.gradle')
